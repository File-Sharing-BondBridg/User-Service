plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.7'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id "org.sonarqube" version "7.0.1.6134"
    id 'com.diffplug.spotless' version '6.25.0'
}

group = 'com.file-sharing'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat('1.22.0')  // Latest stable
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
    ratchetFrom 'origin/main'  // Only enforce on new/changed files in PRs
}

sonar {
    properties {
        property "sonar.projectKey", "File-Sharing-BondBridg_User-Service"
        property "sonar.organization", "File-Sharing-BondBridg"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.scm.revision", System.getenv("GITHUB_SHA")
    }
}

jacoco {
    toolVersion = "0.8.11"
}

tasks.test {
    useJUnitPlatform()
    finalizedBy("jacocoTestReport")
}

tasks.jacocoTestReport {
    dependsOn(tasks.test)
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'io.nats:jnats:2.16.14'
    implementation 'org.springframework.boot:spring-boot-starter'

    runtimeOnly 'org.postgresql:postgresql'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}
